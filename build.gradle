import org.gradle.api.publish.maven.MavenPublication

import java.text.SimpleDateFormat

plugins {
    id 'org.jetbrains.changelog'
    id 'net.researchgate.release'
}

apply plugin: 'java-library'
apply plugin: 'groovy'
apply plugin: 'maven-publish'

group 'dk.sunepoulsen.tes'
version "${project.property('version')}"

repositories {
    mavenLocal()
    mavenCentral()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

dependencies {
    // Tech Easy Solutions
    api "${project.group}:tes-rest-models:1.0.1-SNAPSHOT"
    api "${project.group}:tes-json:1.0.1-SNAPSHOT"

    // Rx Java2
    api 'io.reactivex.rxjava3:rxjava:3.1.5'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    // Logging
    implementation 'ch.qos.logback:logback-classic:1.2.11'
    implementation 'org.slf4j:log4j-over-slf4j:1.7.36'

    // JUnit
    testImplementation 'org.junit.vintage:junit-vintage-engine:5.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.0'

    // Spock
    testImplementation 'org.codehaus.groovy:groovy-all:3.0.11'
    testImplementation 'org.spockframework:spock-core:2.1-groovy-3.0'
    testImplementation 'cglib:cglib-nodep:3.3.0'
    testImplementation 'org.objenesis:objenesis:3.2'

    // WireMock
    testImplementation 'com.github.tomakehurst:wiremock:2.27.2'
}

test {
    useJUnitPlatform()
}

task allWrappers(type: Wrapper) {
    gradleVersion = "${project.property('gradle.version')}"
}

changelog {
    path = "${project.projectDir}/CHANGELOG.md"
    header = "${-> version.get()} - ${new SimpleDateFormat("yyyy-MM-dd").format(new Date())}"
    headerParserRegex = ~/(\d+\.\d+\.\d+.+)/
    itemPrefix = "-"
    keepUnreleasedSection = true
    unreleasedTerm = "Unreleased work"
    groups = ["Features", "Fixed", "Security"]
}

release {
    git {
        requireBranch = 'main'
    }
}
